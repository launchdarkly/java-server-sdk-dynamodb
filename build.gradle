
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

plugins {
    id "java"
    id "java-library"
    id "checkstyle"
    id "signing"
    id "maven-publish"
    id "de.marcphilipp.nexus-publish" version "0.3.0"
    id "io.codearte.nexus-staging" version "0.30.0"
    id "idea"
}

configurations.all {
    // check for updates every build for dependencies with: 'changing: true'
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    mavenLocal()
    // Before LaunchDarkly release artifacts get synced to Maven Central they are here along with snapshots:
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    mavenCentral()
}

allprojects {
    group = 'com.launchdarkly'
    version = "${version}"
    archivesBaseName = 'launchdarkly-java-server-sdk-dynamodb-store'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

ext.versions = [
    "sdk": "6.0.0-SNAPSHOT", // the *lowest* version we're compatible with
    "dynamodb": "2.10.32"
]

ext.libraries = [:]

dependencies {
    api "com.launchdarkly:launchdarkly-java-server-sdk:${versions.sdk}"
    api "software.amazon.awssdk:dynamodb:${versions.dynamodb}"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "junit:junit:4.12"
    testImplementation "com.launchdarkly:launchdarkly-java-server-sdk:${versions.sdk}:test" // our unit tests use helper classes from the SDK
    testImplementation "com.google.guava:guava:28.2-jre" // required by SDK tests, not used in this library itself
    testImplementation "com.google.code.gson:gson:2.7" // same as above
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

checkstyle {
    configFile = file("${project.rootDir}/checkstyle.xml")
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

nexusStaging {
    packageGroup = "com.launchdarkly"
    numberOfRetries = 40 // we've seen extremely long delays in closing repositories
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'com.launchdarkly'
            artifactId = project.archivesBaseName
            
            artifact sourcesJar
            artifact javadocJar
            
            pom {
                name = project.archivesBaseName
                description = 'LaunchDarkly Java SDK DynamoDB integration'
                url = 'https://github.com/launchdarkly/java-server-sdk-dynamodb'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'LaunchDarkly'
                        email = 'team@launchdarkly.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/launchdarkly/java-server-sdk-dynamodb.git'
                    developerConnection = 'scm:git:ssh:git@github.com:launchdarkly/java-server-sdk-dynamodb.git'
                    url = 'https://github.com/launchdarkly/java-server-sdk-dynamodb'
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

nexusPublishing {
    clientTimeout = java.time.Duration.ofMinutes(3) // we've seen extremely long delays in creating repositories
    repositories {
        sonatype {
            username = ossrhUsername
            password = ossrhPassword            
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { !"1".equals(project.findProperty("LD_SKIP_SIGNING")) } // so we can build jars for testing in CI
}
